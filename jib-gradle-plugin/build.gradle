plugins {
  id 'java-gradle-plugin'
  id 'net.researchgate.release'
  id 'com.gradle.plugin-publish'
  id 'maven-publish'
}

repositories {
  // because gradle plugin dependencies are pulling from jcenter
  jcenter()
}

dependencies {
  // for gradle plugin development
  implementation gradleApi()

  implementation 'com.google.http-client:google-http-client'
  implementation 'com.fasterxml.jackson.core:jackson-databind'
  implementation 'com.google.guava:guava'
  sourceProject project(":jib-core")
  sourceProject project(":jib-plugins-common")

  testImplementation 'junit:junit'
  testImplementation 'org.mockito:mockito-core'
  testImplementation 'org.slf4j:slf4j-api'
  testImplementation project(path:':jib-plugins-common', configuration:'tests')
  integrationTestImplementation project(path:':jib-core', configuration:'integrationTests')

  // only for testing a concrete Spring Boot example in a test (not for test infrastructure)
  testImplementation 'org.springframework.boot:spring-boot-gradle-plugin:2.1.6.RELEASE'
}

/* RELEASE */
configurePomProperties()

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = 'Jib'
        description = 'A Gradle plugin for building container images for your Java applications.'
      }
    }
  }
}

// Prepare release
release {
  tagTemplate = 'v$version-gradle'
  git {
    requireBranch = /^gradle_release_v\d+.*$/  //regex
  }
}
// Gradle Plugin Portal releases
pluginBundle {
  website = 'https://github.com/GoogleContainerTools/jib/'
  vcsUrl = 'https://github.com/GoogleContainerTools/jib/'

  plugins {
    jibPlugin {
      id = 'com.google.cloud.tools.jib'
      displayName = 'Jib'
      description = 'Containerize your Java application'
      tags = ['google', 'java', 'containers', 'docker', 'kubernetes', 'microservices']
    }
  }
}
//tasks.publishPlugins.dependsOn integrationTest
/* RELEASE */
